plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'checkstyle'
}

group = 'by.sadovnick'
version = '0.0.1-SNAPSHOT'
description = 'communicator'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

checkstyle {
    toolVersion = "10.12.5"
    configFile = 'checkstyle/checkstyle.xml' as File
    checkstyleTest.enabled = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.1")
}

def mapstructVersion = '1.6.3'
def hypersistenceVersion = '3.9.4'
def nimbusJoseJwtVersion = '10.6'
def logstashLogbackEncoderVersion = '9.0'
def bouncycastleVersion = '1.79'
def igniteVersion = '3.1.0'

def dir = layout.buildDirectory

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    //config-processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.liquibase:liquibase-core'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    //mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    implementation "io.hypersistence:hypersistence-utils-hibernate-60:${hypersistenceVersion}"
    implementation "com.nimbusds:nimbus-jose-jwt:${nimbusJoseJwtVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"
    implementation "org.bouncycastle:bcpkix-jdk15to18:${bouncycastleVersion}"
    implementation "org.bouncycastle:bcprov-jdk15to18:${bouncycastleVersion}"
    implementation "org.apache.ignite:ignite-core:${igniteVersion}"
    runtimeOnly 'org.postgresql:postgresql'
    //test block
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'io.github.openfeign:feign-mock'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jacoco {
    toolVersion = "0.8.14"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

def filteredClassDirs = {
    FileCollection classDirs, List<String> excludes ->
        files(classDirs.collect{
            d ->
                fileTree(dir: d, exclude: excludes)
        })
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = dir.dir('jacoco')
    }
    classDirectories.setFrom(
            filteredClassDirs(
                    classDirectories,
                    [
                            "**/CommunicatorApplication*"
                    ]
            )
    )
    doLast {
        println("Coverage test report: file://" + dir.get() + "/jacoco/index.html")
    }
    finalizedBy(tasks.jacocoTestCoverageVerification)
}

jacocoTestCoverageVerification {
    enabled = false
    dependsOn(tasks.jacocoTestReport)
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
    classDirectories.setFrom(
            filteredClassDirs(
                    classDirectories,
                    [
                            "**/CommunicatorApplication*"
                    ]
            )
    )
}